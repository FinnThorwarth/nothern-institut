###
## Add your overrides to 3rd party fusion code here
###

prototype(WG.Site:Row) < prototype(Neos.Fusion:Tag) {
	attributes.class = ${'grid-x grid-padding-x grid-padding-y' + q(node).property('large') + ' ' + q(node).property('additionalClass')}
	content = Neos.Neos:ContentCollectionRenderer
	# Add contentElementWrapping so it would be possible to edit row's properties
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}

prototype(WG.Site:Column) < prototype(Neos.Fusion:Tag) {
	iterator = ${iterator}
	attributes.class = ${'cell ' + q(node).property('large') + ' ' + q(node).property('medium') + ' ' + q(node).property('small') + ' ' + q(node).property('largeOffset') + ' ' + q(node).property('mediumOffset') + ' ' + q(node).property('smallOffset') + ' ' + q(node).property('additionalClass')}
	content = Neos.Neos:ContentCollectionRenderer
	# Add contentElementWrapping so it would be possible to edit column's properties
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}


# Two Column TS Object
prototype(WG.Site:TwoColumn) >
prototype(WG.Site:TwoColumn) < prototype(WG.Site:MultiColumn)

# Three Column TS Object
prototype(WG.Site:ThreeColumn) >
prototype(WG.Site:ThreeColumn) < prototype(WG.Site:MultiColumn)

# Four Column TS Object
prototype(WG.Site:FourColumn) >
prototype(WG.Site:FourColumn) < prototype(WG.Site:MultiColumn)


##
# Error matcher to render 4xx status code
#
error {
      404 {
		@position = 'start'
        condition = ${statusCode == 404}
        renderer = WG.Site:Document.AbstractPage {
            @context.documentNode = ${site}
            @context.node = ${site}
            prototype(Neos.Seo:TitleTag) {
                title = ${I18n.translate('404 die Seite wurde nicht gefunden')}
            }
            body.content = Neos.Fusion:Component {
                renderer = afx`
                    <main class="main">
                      <div class="grid-container">
                        <div class="grid-x grid-padding-y">
                            <div class="cell">
                            <h1>404 Seite nicht gefunden</h1>
                            <p>Die von Ihnen aufgerufene Seite ist nicht oder nicht mehr verfügbar.</p>
                            <a href="/">Hier gehts zurück zur Startseite ></a>
                            <br /><br /><br /><br />
                          </div>
                        </div>
                      </div>
                    </main>
                `
            }
        }
    }
      4xx {
		@position = 'start'
        condition = ${statusCode >= 400 && statusCode < 500}
        renderer = WG.Site:Document.AbstractPage {
            @context.documentNode = ${site}
            @context.node = ${site}
            prototype(Neos.Seo:TitleTag) {
                title = ${I18n.translate('WG.Site:Main:4xx.errorMessage')}
            }
            body.content = Neos.Fusion:Component {
                renderer = afx`
                    <main class="main">
                        <h1>{I18n.translate('WG.Site:Main:4xx.errorMessage')}</h1>
                        <a href="/">{I18n.translate('WG.Site:Main:4xx.backHome')}</a>
                        <br /><br /><br /><br />
                    </main>
                `
            }
        }
    }
}
